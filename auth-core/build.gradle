import com.chrynan.auth.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    targets {
        jvm()
    }
    sourceSets {
        all {
            languageSettings {
                languageSettings.enableLanguageFeature("InlineClasses")
            }
        }
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"

                // Kotlinx Serialization
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0-RC"

                // Kotlinx Coroutines
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"

                // UUID
                implementation "com.benasher44:uuid:0.3.1"
            }
        }
    }
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    repositories {
        maven {
            url "https://repo.repsy.io/mvn/chrynan/public"

            credentials {
                username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
            }
        }
    }
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
