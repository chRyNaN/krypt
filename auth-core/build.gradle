import com.chrynan.auth.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    targets {
        jvm()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation("org.jetbrains.kotlin:kotlin-stdlib-common")

                // Kotlinx Serialization
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0-RC")

                // Kotlinx Coroutines
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2")

                // UUID
                implementation("com.benasher44:uuid:0.3.1")

                // IO/Encryption
                implementation("com.squareup.okio:okio:3.0.0-alpha.10")

                // Redacted Wrapper
                implementation("com.chrynan.redacted:redacted-core:0.1.2")
            }
        }
        jvmMain {
            dependencies {
                // Password Hashing
                implementation("org.springframework.security:spring-security-crypto:5.5.2")
                implementation("org.bouncycastle:bcprov-jdk15on:1.69")
            }
        }
    }
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    repositories {
        maven {
            url "https://repo.repsy.io/mvn/chrynan/public"

            credentials {
                username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
            }
        }
    }
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
